plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0'
}

build {
    dependsOn(shadowJar)
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

shadowJar {
    relocate('com.google.gson', 'me.devflare.voidgen.libs.gson')
    relocate('com.tcoded.folialib', "me.devflare.voidgen.libs.folialib")
    archiveClassifier = ''
}

repositories {
    mavenCentral()
    gradlePluginPortal()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = "multiverse"
        url = "https://repo.onarandombox.com/content/groups/public/"
    }
    maven {
        name = "tcoded-releases"
        url = "https://repo.tcoded.com/releases"
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.21.1-R0.1-SNAPSHOT'
    compileOnly 'org.projectlombok:lombok:1.18.36'
    compileOnly 'org.mvplugins.multiverse.core:multiverse-core:5.0.0-SNAPSHOT'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    implementation 'com.google.code.gson:gson:2.13.0'
    implementation 'com.tcoded:FoliaLib:0.5.1'
}

processResources {
    def properties = [version: project.plugin_version, name: project.plugin_name,
                      group  : project.plugin_group, website: project.plugin_website,
                      author : project.plugin_author]
    inputs.properties properties
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand properties
    }
}

group = project.plugin_group as Object
version = project.plugin_version as Object